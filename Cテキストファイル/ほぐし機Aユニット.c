'ほぐし機Aユニット
'===================================================================================================================
'
'<<<<<<<	ユニット	>>>>>>>>
'
'===================================================================================================================
'
'	ほぐし機Aユニット
'
' PH31_釜検知 → PH67_釜検知
'===================================================================================================================
客先コード=川崎北部
'===================================================================================================================
select case 客先コード

case 川崎南部
	ADRSET(PH31_釜検知.B,pPH_釜検知.D)

case 川崎北部
	ADRSET(PH67_釜検知.B,pPH_釜検知.D)

end select

'===================================================================================================================
'【 初期化処理 】
if CR2008 then
    ほぐし機AユニットAutoMode=ON '常に自動搬送AutoMode ONLINE、OFFLINEなし
    ほぐし機AユニットAutoRun=OFF
    ほぐし機Aユニット許可=ON
    ほぐし機AユニットOrgErr=OFF
    ほぐし機AユニットErr=OFF
    ほぐし機AユニットEmg=OFF
    ほぐし機Aユニットstep=0
    ほぐし機AユニットErrStep=0
    ほぐし機AユニットOrgErrStep=0
    ほぐし機AユニットEmgStep=0
	
end if
'===================================================================================================================
'【 原点 】
ほぐし機AユニットOrg=(反転ほぐし機A禁止 or 釜搬入可能信号_A) and ((反転ほぐし釜供給中A and *pPH_釜検知.B) or (not(反転ほぐし釜供給中A) and not(*pPH_釜検知.B)) ) 

'===================================================================================================================
'【 緊急停止 】

if not(反転ほぐし機A禁止) and 反転機異常信号_A then

	if ldp(反転機異常信号_A) then
		alm反転機異常信号_A=ON
	end if 
	 
	 ほぐし機AユニットErrStep=1
	 
end if
'===================================================================================================================
'pnlPB反転機運転指令_A	ビット	R71501 反転ほぐし全釜反転
'pnlPB反転機停止指令_A	ビット	R71502 反転ほぐし機A禁止

if 自動搬送AutoRun then
	if ldp(pnlPB反転機運転指令_A) and not(反転ほぐし機A禁止) then
		if not(反転ほぐし全釜反転) and 釜搬入可能信号_A  then
			反転ほぐし全釜反転=ON
		else if 反転ほぐし全釜反転 then
			反転ほぐし全釜反転=OFF	
		end if
		
	else if ldp(pnlPB反転機停止指令_A) then
		if not(反転ほぐし機A禁止) then
			反転ほぐし機A禁止=ON
			反転ほぐし全釜反転=OFF	
		else if 反転ほぐし機A禁止 and 釜搬入可能信号_A then
			反転ほぐし機A禁止=OFF	
		end if

	end if
else
	if ldp(pnlPB反転機運転指令_A) then
		if not(反転ほぐし全釜反転) then
			反転ほぐし全釜反転=ON
		else if 反転ほぐし全釜反転 then
			反転ほぐし全釜反転=OFF	
		end if
	else if ldp(pnlPB反転機停止指令_A) then
		if not(反転ほぐし機A禁止) then
			反転ほぐし機A禁止=ON
			反転ほぐし全釜反転=OFF	
		else if 反転ほぐし機A禁止 then
			反転ほぐし機A禁止=OFF	
		end if
	end if
end if

'===================================================================================================================
'反転ほぐし機を動作させない。
if 0 then
StageDevice41=99
end if
'===================================================================================================================
'【 タイムエラー検出および計測エラー検出】

'===================================================================================================================
'【 System条件 】
'次のフラグはリードコマンド（書き換え不可）
'System ::  ほぐし機AユニットAutoMode	自動モード セレクトスイッチ
'System ::  ほぐし機AユニットAutoRun	継続運転可能
'System :: 浸漬Aユニット許可 該当ユニット動作可能 
'System ::  ほぐし機AユニットEmg	その場停止


if  ほぐし機AユニットEmgStep=0 then  
	
	if ほぐし機AユニットErrStep=0  then

	if not(ほぐし機AユニットOrgErr) then

	if  (ほぐし機AユニットAutoMode and 自動搬送AutoMode ) or ほぐし機Aユニットstep<>0 then
	
		if not(ほぐし機AユニットAutoMode and 自動搬送AutoMode ) then
			ほぐし機AユニットAutoRun=OFF
		end if
		

	'===================================================================================================================
	'【 プロセス処理 】
	'
		tmr(tim_procstep,tim_procstep_val)

		select case ほぐし機Aユニットstep
		'--------------------------------------------------------------------------------------------------------------
		case 0	' 終了待機
		'--------------------------------------------------------------------------------------------------------------
		case 1	' 実行
		'--------------------------------------------------------------------------------------------------------------
		' 作業工程を振り分ける
		'--------------------------------------------------------------------------------------------------------------
		' [ 自動継続運転 ]
			if  ほぐし機AユニットAutoRun and 自動搬送AutoRun then
				inc(ほぐし機Aユニットstep)
			else 
				ほぐし機Aユニットstep=0 '終了待機
			end if
				
		case 2
			'ほぐし機が停止中に受付
			if 反転ほぐし機A禁止 then
				inc(ほぐし機Aユニットstep)			
			
			else if RLY_反転機電源確認 and 釜搬入可能信号_A then
				inc(ほぐし機Aユニットstep)
			
			else			
				ほぐし機Aユニットstep=1
			end if
			
		
		case 3
			if ほぐし機Aユニット完了 then
				if *pPH_釜検知.B then
					ほぐし機Aユニット排出要求=ON
					ほぐし機Aユニットstep=1
				else if 反転ほぐし機A禁止 then
					ほぐし機Aユニット完了=ON
				else 
					inc(ほぐし機Aユニットstep)
				end if
			else
				inc(ほぐし機Aユニットstep)
			end if

		case 4
			if 反転ほぐし釜供給中A and not(ほぐし機Aユニット排出要求) and not(ほぐし機Aユニット完了) then
				inc(ほぐし機Aユニットstep)
			else 
				ほぐし機Aユニットstep=1
			end if
	
		case 5
			if 反転ほぐし機A禁止 then
				inc(ほぐし機Aユニットstep)			
			else if ほぐし機Aユニット許可 then
				inc(ほぐし機Aユニットstep)
			else
				ほぐし機Aユニット排出要求=ON
				ほぐし機Aユニット完了=ON
				ほぐし機Aユニットstep=1
			end if

		case 6
			if 反転ほぐし機A禁止 then
				inc(ほぐし機Aユニットstep)

			else if *pPH_釜検知.B and (反転ほぐし全釜反転 or (0<ANDA(StageDevice41,$FF) and  ANDA(StageDevice41,$FF)<99))  then 'PH_釜有り and 炊飯釜
				ほぐし機Aユニット動作中=ON
				INC(DM_排出総数)

				反転機運転指令_A=ON '--指令ON				
				tim_procstep_val=2 '200mSec出力
				res(tim_procstep)
				inc(ほぐし機Aユニットstep)
			else
				ほぐし機Aユニット排出要求=ON
				ほぐし機Aユニット完了=ON
				ほぐし機Aユニットstep=1
			end if
		
		case 7
			if 反転ほぐし機A禁止 then
				inc(ほぐし機Aユニットstep)			
			else if tim_procstep.B then
				反転機運転指令_A=OFF '--指令OFF
				inc(ほぐし機Aユニットstep)
			end if
		
		case 8
			if 反転ほぐし機A禁止 then
				inc(ほぐし機Aユニットstep)			
			else if not(反転機反転完了信号_A)  then
				tim_procstep_val=10
				res(tim_procstep)
				inc(ほぐし機Aユニットstep)			
			end if
		
		case 9
			if 反転ほぐし機A禁止 then
				inc(ほぐし機Aユニットstep)			
			else if tim_procstep.B then
				if 反転機反転完了信号_A  then
					inc(ほぐし機Aユニットstep)			
				end if
			end if
			
		case 10
			ほぐし機Aユニット動作中=OFF
			ほぐし機Aユニット排出要求=ON
			ほぐし機Aユニット完了=ON
			ほぐし機Aユニットstep=1

		
		end select
		
	'===================================================================================================================
	'【 手動スイッチマニュアル操作 】
	'  マニュアルスイッチ操作はタイムエラーを発生させない
	else 
		 ほぐし機AユニットAutoRun=OFF	
		ほぐし機Aユニットstep=0 '終了

	
	end if ' ほぐし機AユニットAutoMode and not(ほぐし機AユニットOrgErrStep)

	'===================================================================================================================
	'【 ほぐし機AユニットOrgErr処理 】
	else
	
		if ResetFlg then
			ほぐし機AユニットOrgErr=OFF
		end if

	end if

	'===================================================================================================================
	'【 タイムアウトエラー処理 】
	' タイムアウトエラーは”ResetFlg"発効後、 継続運転が可能
	else
		ほぐし機AユニットErr=ON
		
		select case ほぐし機AユニットErrStep
		case 0
		case 1
			if ResetFlg then
				inc(ほぐし機AユニットErrStep)
			end if
		case 2
			ほぐし機AユニットErr=OFF
			
			ほぐし機AユニットErrStep=0
		end select

		
	end if 'if ほぐし機AユニットErrStep=0 then
	
	'===================================================================================================================
	'【 非常停止処理（その場停止） 】
else
	select case ほぐし機AユニットEmgStep
	case 0
	
	case 1
		ほぐし機AユニットAutoRun=OFF
		'---------------------------------
		'緊急停止処理		
		'---------------------------------
		if  (ほぐし機AユニットAutoMode and 自動搬送AutoMode ) or ほぐし機Aユニットstep<>0 then
			'---------------------------------
			'緊急停止処理(自動 ワンサイクル)		
			'---------------------------------
			if timstack=OFF then
				tim_procstep_stack=tim_procstep.U
				timstack=ON
			end if
			'---------------------------------
		
		else
			'---------------------------------
			'緊急停止処理(手動）		
			'---------------------------------
		
			'---------------------------------
		end if
		
		'---------------------------------
		inc(ほぐし機AユニットEmgStep)
	case 2
		if ResetFlg then
			alm反転機異常信号_A=OFF
			almRLY_反転機電源確認=OFF
			inc(ほぐし機AユニットEmgStep)
		end if
	case 3
		if  (ほぐし機AユニットAutoMode and 自動搬送AutoMode ) or ほぐし機Aユニットstep<>0 then
			if StartFlg then
				'---------------------------------
				'緊急停止解除(自動  ワンサイクル)
				'---------------------------------				
				tim_procstep.U=tim_procstep_stack
				
				
				'---------------------------------
				inc(ほぐし機AユニットEmgStep)
			end if
		else
			'---------------------------------
			'緊急停止解除(手動)
			'---------------------------------
			
			
			
			'---------------------------------
			inc(ほぐし機AユニットEmgStep)		
		end if				
	case 4
		timstack=OFF
		ほぐし機AユニットEmgStep=0
		ほぐし機AユニットEmg=OFF
	end select
		
end if 'if  ほぐし機AユニットEmgStep=0 then
